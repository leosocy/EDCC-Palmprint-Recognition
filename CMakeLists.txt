########################
#  EDCC library cmake  #
########################

CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

PROJECT(EDCC)
SET(CMAKE_MACOSX_RPATH 1)
SET(PROJECT_ROOT ${CMAKE_SOURCE_DIR})
SET(LIBRARY_NAME edcc)

SET(${PROJECT_NAME}_VERSION_MAJOR "0")
SET(${PROJECT_NAME}_VERSION_MINOR "2")
SET(${PROJECT_NAME}_VERSION_PATCH "0")
SET(${PROJECT_NAME}_VERSION
  "${${PROJECT_NAME}_VERSION_MAJOR}.${${PROJECT_NAME}_VERSION_MINOR}.${${PROJECT_NAME}_VERSION_PATCH}")


# Project Options
OPTION(${PROJECT_NAME}_BUILD_TESTS "Enable testing" OFF)
OPTION(${PROJECT_NAME}_BUILD_SHARED_LIBS "Build Shared Libraries" ON)


# Required libraries
FIND_PACKAGE(OpenCV REQUIRED)

IF(${PROJECT_NAME}_BUILD_TESTS)
  SET(GCC_COVERAGE_COMPILE_FLAGS "-ggdb -coverage -fprofile-arcs -ftest-coverage")
  SET(GCC_COVERAGE_LINK_FLAGS    "-coverage -lgcov")
ENDIF()
SET(CMAKE_CXX_FLAGS  "--std=c++11 ${CMAKE_CXX_FLAGS} ${GCC_COVERAGE_COMPILE_FLAGS}")
SET(CMAKE_EXE_LINKER_FLAGS  "${CMAKE_EXE_LINKER_FLAGS} ${GCC_COVERAGE_LINK_FLAGS}")
SET(CMAKE_EXPORT_COMPILE_COMMANDS ON)


# Sources, headers, directories and libs
SET(HEADER_DIRECTORY "include/edcc/")

FILE(GLOB_RECURSE SRC_FILES "src/[a-zA-Z]*.cpp")
FILE(GLOB_RECURSE PUBLIC_HEADER_FILES "${HEADER_DIRECTORY}[a-zA-Z]*.h")
FILE(GLOB_RECURSE PRIVATE_HEADER_FILES "src/[a-zA-Z]*.h")

SET(LIBRARY_SOURCES
  ${SRC_FILES}
  ${PUBLIC_HEADER_FILES}
  ${PRIVATE_HEADER_FILES}
)

IF (CMAKE_VERSION VERSION_LESS 2.8.12)
  INCLUDE_DIRECTORIES(${PROJECT_ROOT}/include)
  INCLUDE_DIRECTORIES(${PROJECT_ROOT}/src)
ENDIF()


# General compilation settings
SET(${PROJECT_NAME}_C_FLAGS ${CMAKE_C_FLAGS})
SET(${PROJECT_NAME}_CXX_FLAGS ${CMAKE_CXX_FLAGS})

IF(${PROJECT_NAME}_BUILD_SHARED_LIBS)
  SET(LABEL_SUFFIX "SHARED")
ELSE()
  SET(LABEL_SUFFIX "STATIC")
ENDIF()


# General install settings
SET(INCLUDE_INSTALL_ROOT_DIR include)

SET(INCLUDE_INSTALL_DIR ${INCLUDE_INSTALL_ROOT_DIR}/${LIBRARY_NAME})
SET(LIB_INSTALL_DIR "lib${LIB_SUFFIX}")

SET(_INSTALL_DESTINATIONS
  RUNTIME DESTINATION bin
  LIBRARY DESTINATION ${LIB_INSTALL_DIR}
  ARCHIVE DESTINATION ${LIB_INSTALL_DIR}
)


# Library
ADD_LIBRARY(${LIBRARY_NAME} ${LABEL_SUFFIX} ${LIBRARY_SOURCES})
SET(LINK_LIBRARIES ${OpenCV_LIBS})
TARGET_LINK_LIBRARIES(${LIBRARY_NAME} ${LINK_LIBRARIES})

IF(NOT CMAKE_VERSION VERSION_LESS 2.8.12)
  TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME}
    PUBLIC $<BUILD_INTERFACE:${PROJECT_ROOT}/include>
           $<INSTALL_INTERFACE:${INCLUDE_INSTALL_ROOT_DIR}>
    PRIVATE $<BUILD_INTERFACE:${PROJECT_ROOT}/src>
  )
ENDIF()

SET_TARGET_PROPERTIES(${LIBRARY_NAME} PROPERTIES
  COMPILE_FLAGS "${${PROJECT_NAME}_C_FLAGS} ${${PROJECT_NAME}_CXX_FLAGS}"
)

SET_TARGET_PROPERTIES(${LIBRARY_NAME} PROPERTIES
  VERSION "${${PROJECT_NAME}_VERSION}"
  SOVERSION "${${PROJECT_NAME}_VERSION_MAJOR}.${${PROJECT_NAME}_VERSION_MINOR}"
  PROJECT_LABEL "${LIBRARY_NAME} ${LABEL_SUFFIX}"
)

INSTALL(TARGETS ${LIBRARY_NAME} EXPORT ${LIBRARY_NAME}-targets ${_INSTALL_DESTINATIONS})
INSTALL(
  DIRECTORY ${HEADER_DIRECTORY}
  DESTINATION ${INCLUDE_INSTALL_DIR}
  FILES_MATCHING PATTERN "*.h"
)

EXPORT(
  TARGETS ${LIBRARY_NAME}
  FILE "${PROJECT_BINARY_DIR}/${LIBRARY_NAME}-targets.cmake"
)
EXPORT(PACKAGE EDCC) # EDCCConfig.cmake
SET(EXPORT_TARGETS ${LIBRARY_NAME} CACHE INTERNAL "export targets")

SET(CONFIG_INCLUDE_DIRS "${PROJECT_ROOT}/include")
CONFIGURE_FILE(${PROJECT_ROOT}/${LIBRARY_NAME}-config.cmake.in
  "${PROJECT_BINARY_DIR}/${LIBRARY_NAME}.cmake" @ONLY)

SET(INSTALL_CMAKE_DIR ${LIB_INSTALL_DIR}/cmake/${LIBRARY_NAME})

FILE(RELATIVE_PATH REL_INCLUDE_DIR
  "${CMAKE_INSTALL_PREFIX}/${INSTALL_CMAKE_DIR}"
  "${CMAKE_INSTALL_PREFIX}/${INCLUDE_INSTALL_ROOT_DIR}")
SET(CONFIG_INCLUDE_DIRS "\${${PROJECT_NAME}_CMAKE_DIR}/${REL_INCLUDE_DIR}")
CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/${LIBRARY_NAME}-config.cmake.in
  "${PROJECT_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/${LIBRARY_NAME}-config.cmake" @ONLY)

CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/${LIBRARY_NAME}-config-version.cmake.in
  "${PROJECT_BINARY_DIR}/${LIBRARY_NAME}-config-version.cmake" @ONLY)

INSTALL(FILES
	"${PROJECT_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/${LIBRARY_NAME}-config.cmake"
	"${PROJECT_BINARY_DIR}/${LIBRARY_NAME}-config-version.cmake"
	DESTINATION "${INSTALL_CMAKE_DIR}" COMPONENT dev)
INSTALL(EXPORT ${LIBRARY_NAME}-targets DESTINATION ${INSTALL_CMAKE_DIR})


# Extras
IF(${PROJECT_NAME}_BUILD_TESTS)
	ENABLE_TESTING()
	ADD_SUBDIRECTORY(test)
ENDIF()


# Formatting
ADD_CUSTOM_TARGET(fmt
  COMMAND clang-format --style=file -i ${LIBRARY_SOURCES}
  COMMENT "Running clang-format"
  VERBATIM
)

# Subdirectories
ADD_SUBDIRECTORY(test/)
ADD_SUBDIRECTORY(examples/c_example)
ADD_SUBDIRECTORY(examples/cpp_example)
